#pragma once

// BF16-only GQA prompt kernel. INT8 has an independent kernel body and resource
// policy in gqa_attention_prefill_i8.cuh.
//
//   * Br = 64 query rows and Bc = 64 key columns per CTA tile.
//   * 4 warps / 128 threads; each warp owns 16 query rows of the tile.
//   * Q, K, V staged in 96 KiB of dynamic shared memory (single-buffered), with
//     the cp.async of the next K/V tile overlapped against the current
//     QK / PV tensor-core work (exactly FA's single-buffer overlap pattern).
//   * m16n8k16 bf16 MMA for both S = Q Kᵀ and O += P V, online softmax in exp2.
//
// The op first writes the new chunk K/V into absolute positions in the paged cache,
// then computes causal GQA attention for
// every chunk token over all cached history using bottom-right causal alignment
// (query row i attends to keys [0, base_pos + i]).

#include <math_constants.h>

#include "ops/kernel/gqa_attention_prefill_common.cuh"
#include "ops/kernel/gqa_attention_kv_quant.cuh"
#include "ops/kernel/kv_cache_int4_group64.cuh"

namespace ninfer::ops {

// k16i8 档：K 侧 bf16（2 B/元素、无 scale）+ V 侧 int8 code（每 64 维 1 个 fp16 scale）。
// V 的 code 在这里解成 bf16 写进 smem，PV 的 MMA 与纯 bf16 路径完全一致。
// Int4Cache：K/V 都是对称 int4 打包码 + 每 64 维 1 个 fp16 scale，无旋转。
template <typename Geometry, typename Metadata, bool Int8Value = false, bool Int4Cache = false>
__global__ void gqa_attention_prefill_fill_bf16_kernel(
    const __nv_bfloat16* __restrict__ k, const __nv_bfloat16* __restrict__ v,
    const std::int32_t* __restrict__ positions, Metadata metadata,
    __nv_bfloat16* __restrict__ cache_k, __nv_bfloat16* __restrict__ cache_v,
    __half* __restrict__ k_scale_pages, __half* __restrict__ v_scale_pages, std::int32_t width) {
    constexpr int VecElems = 8; // 8 bf16 == 16 B, matching the cache row alignment.
    const int tokens       = metadata.valid_tokens(width);
    const std::int64_t idx = static_cast<std::int64_t>(blockIdx.x) * blockDim.x + threadIdx.x;
    const std::int64_t n =
        static_cast<std::int64_t>(tokens) * Geometry::KVHeads * (kGqaPrefillHeadDim / VecElems);
    if (idx >= n) { return; }

    const int vec                   = static_cast<int>(idx % (kGqaPrefillHeadDim / VecElems));
    const int tmp                   = static_cast<int>(idx / (kGqaPrefillHeadDim / VecElems));
    const int kv_head               = tmp % Geometry::KVHeads;
    const int token                 = tmp / Geometry::KVHeads;
    const int d                     = vec * VecElems;
    const int position              = positions[0] + token;
    const int lane                  = static_cast<int>(threadIdx.x) & 31;
    const std::int32_t* block_table = metadata.block_table();
    int physical_page               = lane == 0 ? paged_kv_physical_page(block_table, position) : 0;
    const std::int64_t src_off =
        static_cast<std::int64_t>(d) +
        static_cast<std::int64_t>(kGqaPrefillHeadDim) * (kv_head + Geometry::KVHeads * token);
    const int4 k_value = load_vec<int4>(&k[src_off]);
    const int4 v_value = load_vec<int4>(&v[src_off]);

    physical_page = __shfl_sync(0xffffffffu, physical_page, 0);

    const std::int64_t cache_off = paged_kv_element_offset<kGqaPrefillHeadDim, Geometry::KVHeads>(
        physical_page, kv_head, position & kPagedKVPageMask, d);
    // int4 档的 cache_k 是 U8 码平面（128 B/token/head），这里按 bf16 步长直写会以 4 倍
    // 步长涂鸦码平面并在大窗口（> 容量/4）时冲出 plane 尾、污染 arena 邻居。这档的 K 只由
    // 下面的量化分支写，bf16 直写必须跳过。
    if constexpr (!Int4Cache) { store_vec(&cache_k[cache_off], k_value); }

    if constexpr (Int4Cache) {
        // int4 档：K/V 同档量化，口径照 int8（8-lane 组内 xor 归约 absmax → fp16
        // scale = absmax/7 → 倒数 → rint 对称钳位 ±7），码宽 4 bit、两值/字节、无旋转。
        // 与 int8 分支相同的 warp 布局：一个 warp 覆盖一个 (kv_head, token) 的 32 个
        // 8 元素 chunk，每 8 个 lane 共享一个 64 维组的 scale。
        const int page_off = position & kPagedKVPageMask;
        const int group    = d / kKVCacheInt4Group;
        auto quantize_side = [&](const int4& value, std::uint8_t* codes, __half* scale_pages) {
            const __nv_bfloat16* bf16 = reinterpret_cast<const __nv_bfloat16*>(&value);
            float values[8];
            float lane_absmax = 0.0f;
#pragma unroll
            for (int i = 0; i < VecElems; ++i) {
                values[i]   = __bfloat162float(bf16[i]);
                lane_absmax = fmaxf(lane_absmax, fabsf(values[i]));
            }
            lane_absmax = fmaxf(lane_absmax, __shfl_xor_sync(0xffffffffu, lane_absmax, 1));
            lane_absmax = fmaxf(lane_absmax, __shfl_xor_sync(0xffffffffu, lane_absmax, 2));
            lane_absmax = fmaxf(lane_absmax, __shfl_xor_sync(0xffffffffu, lane_absmax, 4));
            const __half scale =
                __float2half_rn(lane_absmax > 0.0f ? lane_absmax / kKVCacheInt4MaxCode : 0.0f);
            const float s   = __half2float(scale);
            const float inv = s > 0.0f ? 1.0f / s : 0.0f;
            store_vec(codes + kv_cache_int4_code_index<Geometry>(physical_page, kv_head, d,
                                                                 page_off),
                      kv_cache_int4_pack8(values, inv));
            if ((lane & (kKVCacheInt4Group / VecElems - 1)) == 0) {
                scale_pages[gqa_kv_quant_scale_index<Geometry>(physical_page, kv_head, group,
                                                               page_off)] = scale;
            }
        };
        quantize_side(k_value, reinterpret_cast<std::uint8_t*>(cache_k), k_scale_pages);
        quantize_side(v_value, reinterpret_cast<std::uint8_t*>(cache_v), v_scale_pages);
    } else if constexpr (Int8Value) {
        // V 侧 int8（k16i8）：每 64 维 1 个 fp16 scale。head_dim/8 == 32 个 8 元素 chunk
        // 连续排布 ⇒ 同一 warp 的 32 个 lane 正好覆盖一个 (kv_head, token) 向量的 32 个 chunk，
        // 而每 8 个 lane（= 一个 64 维 group）共享一个 scale ⇒ 先在 8-lane 内做三次 xor 归约。
        // 量化/取整口径与 i8 档位完全一致（fp16 舍入后的 scale 取倒数 + round-to-nearest + 对称钳位）。
        const __nv_bfloat16* v_bf16 = reinterpret_cast<const __nv_bfloat16*>(&v_value);
        float lane_absmax           = 0.0f;
#pragma unroll
        for (int i = 0; i < VecElems; ++i) {
            lane_absmax = fmaxf(lane_absmax, fabsf(__bfloat162float(v_bf16[i])));
        }
        lane_absmax = fmaxf(lane_absmax, __shfl_xor_sync(0xffffffffu, lane_absmax, 1));
        lane_absmax = fmaxf(lane_absmax, __shfl_xor_sync(0xffffffffu, lane_absmax, 2));
        lane_absmax = fmaxf(lane_absmax, __shfl_xor_sync(0xffffffffu, lane_absmax, 4));
        const __half v_scale = __float2half_rn(lane_absmax > 0.0f ? lane_absmax / 127.0f : 0.0f);
        const float v_s      = __half2float(v_scale);
        const float v_inv    = v_s > 0.0f ? 1.0f / v_s : 0.0f;
        auto* v_codes        = reinterpret_cast<std::int8_t*>(cache_v);
        int2 packed;
        auto* packed_bytes = reinterpret_cast<std::int8_t*>(&packed);
#pragma unroll
        for (int i = 0; i < VecElems; ++i) {
            packed_bytes[i] = gqa_kv_quant_code(__bfloat162float(v_bf16[i]), v_inv);
        }
        store_vec(&v_codes[cache_off], packed);
        if ((lane & (kGqaKvQuantGroup / VecElems - 1)) == 0) {
            v_scale_pages[gqa_kv_quant_scale_index<Geometry>(
                physical_page, kv_head, d / kGqaKvQuantGroup,
                position & kPagedKVPageMask)] = v_scale;
        }
    } else {
        store_vec(&cache_v[cache_off], v_value);
    }
}

// Stage one [Bc, D] K 或 V tile 的 fp8 版本：cache 里是 8 bit e4m3 code（每 256 维 1 个 fp16
// scale），读 8 个 code（8 B）→ 解量化成 8 个 bf16 → 写进与本文件 bf16 路径完全相同的 swizzle
// 位置。与 bf16 路径的差别：不用 cp.async（要过一遍转换），所以是同步读写（i8 路径同款做法）。
// Stage one [Bc, D] V tile 的 int8 版本（k16i8）：cache 里是 8 bit signed code（每 64 维 1 个
// fp16 scale），读 8 个 code（8 B）→ 解量化成 8 个 bf16 → 写进与本文件 bf16 路径完全相同的
// swizzle 位置。index 与解量化都复用 i8 档位的共享 helper（gqa_attention_kv_quant.cuh），
// 保证与 int8 档位逐位同口径；同样是同步读写（不经过 cp.async）。
template <typename Geometry>
__device__ __forceinline__ void gqa_prefill_stage_i8value(__nv_bfloat16* dst,
                                                         const std::int8_t* cache_codes,
                                                         const __half* scale_pages, int kv_head,
                                                         int k0, int max_query_abs,
                                                         int physical_page, int tid) {
    constexpr int D         = kGqaPrefillHeadDim;
    constexpr int Bc        = kGqaPrefillBc;
    constexpr int Threads   = kGqaPrefillThreads;
    constexpr int VecPerRow = D / 8; // 8 个 int8 code == 8 维
    const bool full_tile    = (k0 + Bc - 1) <= max_query_abs;
    const std::int8_t* cache_block = cache_codes + gqa_kv_quant_code_index<Geometry>(
                                                       physical_page, kv_head, 0,
                                                       k0 & kPagedKVPageMask);
    for (int chunk = tid; chunk < Bc * VecPerRow; chunk += Threads) {
        const int key_l  = chunk >> 5;        // / VecPerRow (32)
        const int d      = (chunk & 31) << 3; // (chunk % 32) * 8
        __nv_bfloat16* p = &dst[key_l * D + gqa_prefill_swz(key_l, d)];
        const int key    = k0 + key_l;
        if (full_tile || key <= max_query_abs) {
            const __half scale = scale_pages[gqa_kv_quant_scale_index<Geometry>(
                physical_page, kv_head, d / kGqaKvQuantGroup, key & kPagedKVPageMask)];
            store_vec(p, gqa_kv_dequant_i8x8_from(&cache_block[key_l * D + d],
                                                  __half2float(scale)));
        } else {
            store_vec(p, make_int4(0, 0, 0, 0));
        }
    }
}

// Stage one [Bc, D] K/V tile 的 int4 版本：cache 里是对称 int4 码（两值/字节，行 128 B）+
// 每 64 维 1 个 fp16 scale（与 int8 档 scale 平面同形）。读 4 B 码（8 维）+ 2 B scale →
// 解成 8 个 bf16 → 写进与本文件 bf16 路径完全相同的 swizzle 位置。同步读写（i8value 同款），
// 无旋转；scale 索引直接复用 int8 的 gqa_kv_quant_scale_index。
template <typename Geometry>
__device__ __forceinline__ void gqa_prefill_stage_int4(__nv_bfloat16* dst,
                                                       const std::uint8_t* cache_codes,
                                                       const __half* scale_pages, int kv_head,
                                                       int k0, int max_query_abs,
                                                       int physical_page, int tid) {
    constexpr int D         = kGqaPrefillHeadDim;
    constexpr int Bc        = kGqaPrefillBc;
    constexpr int Threads   = kGqaPrefillThreads;
    constexpr int VecPerRow = D / 8; // 8 个 int4 码（4 B）== 8 维
    const bool full_tile    = (k0 + Bc - 1) <= max_query_abs;
    const std::uint8_t* cache_block = cache_codes + kv_cache_int4_code_index<Geometry>(
                                                        physical_page, kv_head, 0,
                                                        k0 & kPagedKVPageMask);
    for (int chunk = tid; chunk < Bc * VecPerRow; chunk += Threads) {
        const int key_l  = chunk >> 5;        // / VecPerRow (32)
        const int d      = (chunk & 31) << 3; // (chunk % 32) * 8
        __nv_bfloat16* p = &dst[key_l * D + gqa_prefill_swz(key_l, d)];
        const int key    = k0 + key_l;
        if (full_tile || key <= max_query_abs) {
            const __half scale = scale_pages[gqa_kv_quant_scale_index<Geometry>(
                physical_page, kv_head, d / kKVCacheInt4Group, key & kPagedKVPageMask)];
            store_vec(p, kv_cache_int4_dequant_x8_from(&cache_block[key_l * (D / 2) + d / 2],
                                                       __half2float(scale)));
        } else {
            store_vec(p, make_int4(0, 0, 0, 0));
        }
    }
}

// Stage one [Bc, D] K or V tile from the per-kv-head contiguous cache into the
// swizzled smem buffer. Keys beyond max_query_abs (which the causal mask always
// drops) are zeroed so the padded/uninitialized cache tail never feeds NaNs into
// the tensor cores. Mirrors FA's predicated K/V cp.async + Clear_OOB path.
template <typename Geometry>
__device__ __forceinline__ void gqa_prefill_stage_kv(__nv_bfloat16* dst, const __nv_bfloat16* cache,
                                                     int kv_head, int k0, int max_query_abs,
                                                     int physical_page, int tid) {
    constexpr int D         = kGqaPrefillHeadDim;
    constexpr int Bc        = kGqaPrefillBc;
    constexpr int Threads   = kGqaPrefillThreads;
    constexpr int VecPerRow = D / 8; // 8 bf16 per 16B cp.async
    const bool full_tile    = (k0 + Bc - 1) <= max_query_abs;
    // Block base pointer computed once (int64); per-element offsets stay 32-bit.
    const __nv_bfloat16* cache_block =
        cache + paged_kv_element_offset<kGqaPrefillHeadDim, Geometry::KVHeads>(
                    physical_page, kv_head, k0 & kPagedKVPageMask, 0);
    if (full_tile) {
#pragma unroll
        for (int chunk = tid; chunk < Bc * VecPerRow; chunk += Threads) {
            const int key_l  = chunk >> 5;        // / VecPerRow (32)
            const int d      = (chunk & 31) << 3; // (chunk % 32) * 8
            __nv_bfloat16* p = &dst[key_l * D + gqa_prefill_swz(key_l, d)];
            cp_async<16, Cache::cg>(p, &cache_block[key_l * D + d]);
        }
    } else {
#pragma unroll
        for (int chunk = tid; chunk < Bc * VecPerRow; chunk += Threads) {
            const int key_l  = chunk >> 5;        // / VecPerRow (32)
            const int d      = (chunk & 31) << 3; // (chunk % 32) * 8
            __nv_bfloat16* p = &dst[key_l * D + gqa_prefill_swz(key_l, d)];
            if ((k0 + key_l) <= max_query_abs) {
                cp_async<16, Cache::cg>(p, &cache_block[key_l * D + d]);
            } else {
                store_vec(p, make_int4(0, 0, 0, 0));
            }
        }
    }
}

// FlashAttention-2 forward, one CTA per (query 64-row block, query head). Grid is
// (ceil(tokens/64), q_heads). seqlen_q = tokens, seqlen_k = base_pos + tokens, with
// bottom-right causal alignment (query row i sees keys [0, base_pos + i]).
template <typename Geometry, typename Metadata, bool Int8Value = false, bool Int4Cache = false>
__launch_bounds__(kGqaPrefillThreads, 1) __global__
    void gqa_attention_prefill_bf16_kernel(const __nv_bfloat16* __restrict__ q,
                                           const __nv_bfloat16* __restrict__ cache_k,
                                           const __nv_bfloat16* __restrict__ cache_v,
                                           const __half* __restrict__ k_scale_pages,
                                           const __half* __restrict__ v_scale_pages,
                                           Metadata metadata,
                                           const std::int32_t* __restrict__ positions, float scale,
                                           __nv_bfloat16* __restrict__ out, std::int32_t width) {
    constexpr int D             = kGqaPrefillHeadDim; // 256
    constexpr int Br            = kGqaPrefillBr;      // 64 query rows
    constexpr int Bc            = kGqaPrefillBc;      // 64 key cols
    constexpr int Threads       = kGqaPrefillThreads; // 128
    constexpr int QKNt          = Bc / 8;             // 8  QK score n-tiles
    constexpr int QKKs          = D / 16;             // 16 QK contraction steps over head_dim
    constexpr int PVNt          = D / 8;              // 32 PV output n-tiles
    constexpr int PVKs          = Bc / 16;            // 4  PV contraction steps over keys
    constexpr float Log2E       = 1.4426950408889634074f;
    constexpr unsigned FullMask = 0xffffffffu;

    // GQA-aware 结构约束：每个 head 4 个 warp（每 warp 16 行、合计 64 行 = Br），
    // 一个 CTA 覆盖 kGqaPrefillHeadsPerCta 个 head。
    static_assert(kGqaPrefillWarpsPerHead == 4);
    static_assert(Threads == 32 * kGqaPrefillWarpsPerHead * kGqaPrefillHeadsPerCta);
    static_assert(Br == 16 * kGqaPrefillWarpsPerHead);
    // 一个 Bc 宽的 key tile 必须完整落在一个 paged-KV page 内：tile 的 physical page 按 tile 起始
    // 绝对位置取一次，page 内偏移用 k0 & kPagedKVPageMask。Bc 不能整除 page 就会跨页取到错数据。
    static_assert(kPagedKVPageSize % Bc == 0, "Bc must divide the paged-KV page size");

    extern __shared__ __align__(16) __nv_bfloat16 gqa_smem[];
    __nv_bfloat16* q_s = gqa_smem;     // [Br, D] swizzled
    __nv_bfloat16* k_s = q_s + kGqaPrefillHeadsPerCta * Br * D; // [Bc, D] swizzled
    __nv_bfloat16* v_s = k_s + Bc * D; // [Bc, D] swizzled

    const int q_block    = static_cast<int>(blockIdx.x);
    const int q_head_base = static_cast<int>(blockIdx.y) * kGqaPrefillHeadsPerCta;
    const int tid        = static_cast<int>(threadIdx.x);
    const int warp       = tid >> 5;
    const int lane       = tid & 31;
    const int q0         = q_block * Br;
    // GQA-aware：一个 CTA 覆盖同一 kv_head 组内的 kGqaPrefillHeadsPerCta 个 q_head。
    // 前 kGqaPrefillWarpsPerHead 个 warp 服务第 0 个 head，依次类推；K/V 载入全员共享。
    const int head_slot = warp / kGqaPrefillWarpsPerHead;
    const int q_head    = q_head_base + head_slot;
    const int kv_head   = q_head_base / Geometry::GroupSize;
    const int tokens    = metadata.valid_tokens(width);

    if (q_head_base + kGqaPrefillHeadsPerCta > Geometry::QHeads || q0 >= width) { return; }
    if (q0 >= tokens) {
#pragma unroll
        for (int s = 0; s < kGqaPrefillHeadsPerCta; ++s) {
            gqa_prefill_zero_output_rows<Geometry>(out, q_head_base + s, q0, min(q0 + Br, width),
                                                   tid, Threads);
        }
        return;
    }
    const int base_pos              = positions[0];
    const std::int32_t* block_table = metadata.block_table();

    const int gid = lane >> 2;
    const int lid = lane & 3;

    const int a_mat     = lane >> 3;
    const int a_rin     = lane & 7;
    const int a_rowoff  = a_rin + ((a_mat & 1) << 3);
    const int b_rin     = lane & 7;
    const int b_koff    = ((lane >> 3) & 1) << 3;
    // 本 warp 在自己 head 的 64 行里拥有的 16 行（kGqaPrefillWarpsPerHead == 4 时即 16 行/warp）
    const int warp_row0 = (warp % kGqaPrefillWarpsPerHead) * 16;

    // Per-lane precomputed swizzled ldmatrix base addresses (see gqa_prefill_swz_addr).
    const unsigned q_sbase = smem_addr(q_s);
    const unsigned k_sbase = smem_addr(k_s);
    const unsigned v_sbase = smem_addr(v_s);
    // Q A-fragment: row = warp_row0 + a_rowoff, col = k*16 + a_coloff.
    // 每个 head 一个 Q tile（q_s 里连续排布），head_slot 决定本 warp 读哪一块。
    const unsigned q_lane_base = q_sbase + static_cast<unsigned>(head_slot * Br * D * 2) +
                                 static_cast<unsigned>((warp_row0 + a_rowoff) * 512);
    const unsigned q_as        = static_cast<unsigned>((a_mat >> 1) << 4);
    const unsigned q_r         = static_cast<unsigned>(a_rin << 4);
    // K B-fragment via ldmatrix.x4 (2 n-tiles/instr): lanes 16-31 fetch the +8-key
    // half (extra 4096 bytes), lanes with bit3 set fetch the +8 d-contract half.
    const unsigned k_lane_base =
        k_sbase + static_cast<unsigned>(b_rin * 512) + (static_cast<unsigned>(lane >> 4) << 12);
    const unsigned k_as = static_cast<unsigned>((b_koff >> 3) << 4);
    const unsigned k_r  = static_cast<unsigned>(b_rin << 4);
    // V B-fragment via ldmatrix.x4.trans (2 n-tiles/instr): row = k*16 + (bit3)*8 + b_rin,
    // col = n*8 + (lane>>4)*8.
    const unsigned v_lane_base = v_sbase + static_cast<unsigned>(((lane >> 3) & 1) * 4096) +
                                 static_cast<unsigned>(b_rin * 512);
    const unsigned v_as = static_cast<unsigned>((lane >> 4) << 4);
    const unsigned v_r  = static_cast<unsigned>(b_rin << 4);

    // Stage Q into smem once via cp.async (overlaps with the K(0) prologue load
    // below); it stays resident for the whole key loop. Global Q rows are 256 bf16
    // contiguous, with a token stride of 256*QHeads.
    {
        constexpr int VecPerRow  = D / 8;
        constexpr int QRowStride = D * Geometry::QHeads; // global stride between tokens
#pragma unroll
        for (int h = 0; h < kGqaPrefillHeadsPerCta; ++h) {
            const __nv_bfloat16* q_block = q + gqa_prefill_q_index<Geometry>(q_head_base + h, 0, q0);
            __nv_bfloat16* q_dst         = q_s + h * Br * D;
            if (q0 + Br <= tokens) {
#pragma unroll
                for (int chunk = tid; chunk < Br * VecPerRow; chunk += Threads) {
                    const int row    = chunk >> 5;
                    const int d      = (chunk & 31) << 3;
                    __nv_bfloat16* p = &q_dst[row * D + gqa_prefill_swz(row, d)];
                    cp_async<16, Cache::cg>(p, &q_block[row * QRowStride + d]);
                }
            } else {
#pragma unroll
                for (int chunk = tid; chunk < Br * VecPerRow; chunk += Threads) {
                    const int row    = chunk >> 5;
                    const int d      = (chunk & 31) << 3;
                    __nv_bfloat16* p = &q_dst[row * D + gqa_prefill_swz(row, d)];
                    if (q0 + row < tokens) {
                        cp_async<16, Cache::cg>(p, &q_block[row * QRowStride + d]);
                    } else {
                        store_vec(p, make_int4(0, 0, 0, 0));
                    }
                }
            }
        }
    }

    float acc[PVNt][4];
#pragma unroll
    for (int n = 0; n < PVNt; ++n) {
#pragma unroll
        for (int i = 0; i < 4; ++i) { acc[n][i] = 0.0f; }
    }
    float m0 = -CUDART_INF_F, m1 = -CUDART_INF_F, l0 = 0.0f, l1 = 0.0f;

    const int tile_rows     = min(Br, tokens - q0);
    const int max_query_abs = base_pos + q0 + tile_rows - 1;
    const int n_block_max   = (max_query_abs / Bc) + 1; // n_block_min == 0

    // Fold softmax_scale into the exp2 (FA-style): scores stay raw, so the
    // per-element "* scale" multiply drops out of the QK epilogue entirely.
    const float scale_l2 = scale * Log2E;
    int physical_page    = block_table[0];

    // Prologue: commit Q, then kick off K(0). The loop's wait<0> below drains both.
    ninfer::ops::cp_commit();
    if constexpr (Int4Cache) {
        gqa_prefill_stage_int4<Geometry>(k_s, reinterpret_cast<const std::uint8_t*>(cache_k),
                                         k_scale_pages, kv_head, 0, max_query_abs, physical_page,
                                         tid);
    } else {
        gqa_prefill_stage_kv<Geometry>(k_s, cache_k, kv_head, 0, max_query_abs, physical_page,
                                       tid);
    }

    ninfer::ops::cp_commit();

    for (int kb = 0; kb < n_block_max; ++kb) {
        const int k0                 = kb * Bc;
        const int next_k0            = k0 + Bc;
        const int next_physical_page = (kb + 1 < n_block_max)
                                           ? block_table[next_k0 >> kPagedKVPageShift]
                                           : physical_page;

        ninfer::ops::cp_wait<0>(); // K(kb) landed (also publishes q_s / prev PV done)
        __syncthreads();

        // Overlap V(kb) load against the QK MMA below.
        if constexpr (Int4Cache) {
            gqa_prefill_stage_int4<Geometry>(v_s,
                                             reinterpret_cast<const std::uint8_t*>(cache_v),
                                             v_scale_pages, kv_head, k0, max_query_abs,
                                             physical_page, tid);
        } else if constexpr (Int8Value) {
            gqa_prefill_stage_i8value<Geometry>(v_s, reinterpret_cast<const std::int8_t*>(cache_v),
                                                v_scale_pages, kv_head, k0, max_query_abs,
                                                physical_page, tid);
        } else {
            gqa_prefill_stage_kv<Geometry>(v_s, cache_v, kv_head, k0, max_query_abs,
                                           physical_page, tid);
        }
        ninfer::ops::cp_commit();

        // S = Q Kᵀ for this warp's 16 rows over all Bc keys, in registers.
        // Software-pipelined like cute's gemm: issue the ldmatrix for contraction
        // step k+1 while the m16n8k16 MMAs for step k run, so the LSU (ldmatrix)
        // and tensor pipes overlap instead of stalling on each other.
        float score[QKNt][4];
#pragma unroll
        for (int nt = 0; nt < QKNt; ++nt) {
            score[nt][0] = score[nt][1] = score[nt][2] = score[nt][3] = 0.0f;
        }
        // Swizzled ldmatrix addresses via precomputed per-lane bases + immediates.
        unsigned af[2][4];
        unsigned bf[2][QKNt][2];
        {
            ldmatrix_x4(af[0][0], af[0][1], af[0][2], af[0][3],
                        gqa_prefill_swz_addr(q_lane_base, 0u, q_as, q_r));
#pragma unroll
            for (int nt2 = 0; nt2 < QKNt; nt2 += 2) {
                ldmatrix_x4(bf[0][nt2][0], bf[0][nt2][1], bf[0][nt2 + 1][0], bf[0][nt2 + 1][1],
                            gqa_prefill_swz_addr(k_lane_base + static_cast<unsigned>(nt2 * 4096),
                                                 0u, k_as, k_r));
            }
        }
#pragma unroll
        for (int k = 0; k < QKKs; ++k) {
            const int cur = k & 1;
            const int nxt = cur ^ 1;
            if (k + 1 < QKKs) {
                const unsigned ck = static_cast<unsigned>((k + 1) << 5);
                ldmatrix_x4(af[nxt][0], af[nxt][1], af[nxt][2], af[nxt][3],
                            gqa_prefill_swz_addr(q_lane_base, ck, q_as, q_r));
#pragma unroll
                for (int nt2 = 0; nt2 < QKNt; nt2 += 2) {
                    ldmatrix_x4(
                        bf[nxt][nt2][0], bf[nxt][nt2][1], bf[nxt][nt2 + 1][0], bf[nxt][nt2 + 1][1],
                        gqa_prefill_swz_addr(k_lane_base + static_cast<unsigned>(nt2 * 4096), ck,
                                             k_as, k_r));
                }
            }
#pragma unroll
            for (int nt = 0; nt < QKNt; ++nt) {
                mma_bf16(score[nt][0], score[nt][1], score[nt][2], score[nt][3], af[cur][0],
                         af[cur][1], af[cur][2], af[cur][3], bf[cur][nt][0], bf[cur][nt][1]);
            }
        }

        const int row0             = warp_row0 + gid;
        const int row1             = warp_row0 + gid + 8;
        const int qrow0            = q0 + row0;
        const int qrow1            = q0 + row1;
        const int qabs0            = (qrow0 < tokens) ? base_pos + qrow0 : -1;
        const int qabs1            = (qrow1 < tokens) ? base_pos + qrow1 : -1;
        const bool full_score_tile = (q0 + Br <= tokens) && ((k0 + Bc - 1) <= (base_pos + q0));

        // block row-max on raw (unscaled) scores; scale is folded into exp2 below
        float bm0 = -CUDART_INF_F, bm1 = -CUDART_INF_F;
        if (full_score_tile) {
#pragma unroll
            for (int nt = 0; nt < QKNt; ++nt) {
                bm0 = fmaxf(bm0, fmaxf(score[nt][0], score[nt][1]));
                bm1 = fmaxf(bm1, fmaxf(score[nt][2], score[nt][3]));
            }
        } else {
#pragma unroll
            for (int nt = 0; nt < QKNt; ++nt) {
                const int key0 = k0 + nt * 8 + 2 * lid;
                const int key1 = key0 + 1;
                score[nt][0]   = (qrow0 < tokens && key0 <= qabs0) ? score[nt][0] : -CUDART_INF_F;
                score[nt][1]   = (qrow0 < tokens && key1 <= qabs0) ? score[nt][1] : -CUDART_INF_F;
                score[nt][2]   = (qrow1 < tokens && key0 <= qabs1) ? score[nt][2] : -CUDART_INF_F;
                score[nt][3]   = (qrow1 < tokens && key1 <= qabs1) ? score[nt][3] : -CUDART_INF_F;
                bm0            = fmaxf(bm0, fmaxf(score[nt][0], score[nt][1]));
                bm1            = fmaxf(bm1, fmaxf(score[nt][2], score[nt][3]));
            }
        }
        bm0 = warp_max<4>(bm0, FullMask);
        bm1 = warp_max<4>(bm1, FullMask);

        const float nm0        = fmaxf(m0, bm0);
        const float nm1        = fmaxf(m1, bm1);
        const float nm0_scaled = nm0 * scale_l2;
        const float nm1_scaled = nm1 * scale_l2;
        const float alpha0     = exp2_approx(__fmaf_rn(m0, scale_l2, -nm0_scaled));
        const float alpha1     = exp2_approx(__fmaf_rn(m1, scale_l2, -nm1_scaled));

        // P = exp2(S - m), repacked into the PV A-fragment layout, plus local block row-sum.
        // The row-sum allreduce is deferred to the epilogue; only row max must be reduced per tile.
        float bl0 = 0.0f, bl1 = 0.0f;
        unsigned p_frag[PVKs][4];
        if (full_score_tile) {
#pragma unroll
            for (int nt = 0; nt < QKNt; ++nt) {
                const float p00 = exp2_approx(__fmaf_rn(score[nt][0], scale_l2, -nm0_scaled));
                const float p01 = exp2_approx(__fmaf_rn(score[nt][1], scale_l2, -nm0_scaled));
                const float p10 = exp2_approx(__fmaf_rn(score[nt][2], scale_l2, -nm1_scaled));
                const float p11 = exp2_approx(__fmaf_rn(score[nt][3], scale_l2, -nm1_scaled));
                bl0 += p00 + p01;
                bl1 += p10 + p11;
                const int pk = nt >> 1;
                if ((nt & 1) == 0) {
                    p_frag[pk][0] = pack_bf16x2(p00, p01);
                    p_frag[pk][1] = pack_bf16x2(p10, p11);
                } else {
                    p_frag[pk][2] = pack_bf16x2(p00, p01);
                    p_frag[pk][3] = pack_bf16x2(p10, p11);
                }
            }
        } else {
#pragma unroll
            for (int nt = 0; nt < QKNt; ++nt) {
                const float p00 = (score[nt][0] > -CUDART_INF_F)
                                      ? exp2_approx(__fmaf_rn(score[nt][0], scale_l2, -nm0_scaled))
                                      : 0.0f;
                const float p01 = (score[nt][1] > -CUDART_INF_F)
                                      ? exp2_approx(__fmaf_rn(score[nt][1], scale_l2, -nm0_scaled))
                                      : 0.0f;
                const float p10 = (score[nt][2] > -CUDART_INF_F)
                                      ? exp2_approx(__fmaf_rn(score[nt][2], scale_l2, -nm1_scaled))
                                      : 0.0f;
                const float p11 = (score[nt][3] > -CUDART_INF_F)
                                      ? exp2_approx(__fmaf_rn(score[nt][3], scale_l2, -nm1_scaled))
                                      : 0.0f;
                bl0 += p00 + p01;
                bl1 += p10 + p11;
                const int pk = nt >> 1;
                if ((nt & 1) == 0) {
                    p_frag[pk][0] = pack_bf16x2(p00, p01);
                    p_frag[pk][1] = pack_bf16x2(p10, p11);
                } else {
                    p_frag[pk][2] = pack_bf16x2(p00, p01);
                    p_frag[pk][3] = pack_bf16x2(p10, p11);
                }
            }
        }

        l0 = __fmaf_rn(l0, alpha0, bl0);
        l1 = __fmaf_rn(l1, alpha1, bl1);
        m0 = nm0;
        m1 = nm1;
#pragma unroll
        for (int n = 0; n < PVNt; ++n) {
            acc[n][0] *= alpha0;
            acc[n][1] *= alpha0;
            acc[n][2] *= alpha1;
            acc[n][3] *= alpha1;
        }

        ninfer::ops::cp_wait<0>(); // V(kb) landed; QK done reading k_s
        __syncthreads();

        // Prefetch K(kb+1) into the (now-free) K buffer, overlapping the PV MMA.
        if (kb + 1 < n_block_max) {
            physical_page = next_physical_page;
            if constexpr (Int4Cache) {
                gqa_prefill_stage_int4<Geometry>(
                    k_s, reinterpret_cast<const std::uint8_t*>(cache_k), k_scale_pages, kv_head,
                    (kb + 1) * Bc, max_query_abs, physical_page, tid);
            } else {
                gqa_prefill_stage_kv<Geometry>(k_s, cache_k, kv_head, (kb + 1) * Bc, max_query_abs,
                                               physical_page, tid);
            }

            ninfer::ops::cp_commit();
        }

        // O += P V, contracting over the Bc keys. The (k, n) iteration space is
        // flattened and software-pipelined: the transposed ldmatrix for the next
        // V fragment is issued while the current MMA runs.
        // Each x4.trans load covers 2 output n-tiles (16 dims); pipeline the next
        // load against the current pair of MMAs.
        constexpr int PVHalf  = PVNt / 2;      // 16 n-tile pairs
        constexpr int PVLoads = PVKs * PVHalf; // 64 x4.trans loads
        // Swizzled V x4.trans addresses via precomputed per-lane base + immediates.
        unsigned vf[2][4];
        {
            ldmatrix_x4_t(vf[0][0], vf[0][1], vf[0][2], vf[0][3],
                          gqa_prefill_swz_addr(v_lane_base, 0u, v_as, v_r));
        }
#pragma unroll
        for (int li = 0; li < PVLoads; ++li) {
            const int k   = li / PVHalf;
            const int n2  = (li % PVHalf) * 2;
            const int cur = li & 1;
            const int nxt = cur ^ 1;
            if (li + 1 < PVLoads) {
                const int k2       = (li + 1) / PVHalf;
                const int n2b      = ((li + 1) % PVHalf) * 2;
                const unsigned ckv = static_cast<unsigned>(n2b << 4);
                ldmatrix_x4_t(vf[nxt][0], vf[nxt][1], vf[nxt][2], vf[nxt][3],
                              gqa_prefill_swz_addr(v_lane_base + static_cast<unsigned>(k2 * 8192),
                                                   ckv, v_as, v_r));
            }
            mma_bf16(acc[n2][0], acc[n2][1], acc[n2][2], acc[n2][3], p_frag[k][0], p_frag[k][1],
                     p_frag[k][2], p_frag[k][3], vf[cur][0], vf[cur][1]);
            mma_bf16(acc[n2 + 1][0], acc[n2 + 1][1], acc[n2 + 1][2], acc[n2 + 1][3], p_frag[k][0],
                     p_frag[k][1], p_frag[k][2], p_frag[k][3], vf[cur][2], vf[cur][3]);
        }
    }

    l0 = warp_sum<4>(l0, FullMask);
    l1 = warp_sum<4>(l1, FullMask);

    // Normalize once per row via reciprocal-multiply instead of 128 IEEE divides.
    const float inv_l0 = (l0 > 0.0f) ? __frcp_rn(l0) : 0.0f;
    const float inv_l1 = (l1 > 0.0f) ? __frcp_rn(l1) : 0.0f;
#pragma unroll
    for (int n = 0; n < PVNt; ++n) {
        const int d0    = n * 8 + 2 * lid;
        const int qrow0 = q0 + warp_row0 + gid;
        const int qrow1 = q0 + warp_row0 + gid + 8;
        if (qrow0 < tokens) {
            *reinterpret_cast<unsigned*>(&out[gqa_prefill_q_index<Geometry>(q_head, d0, qrow0)]) =
                pack_bf16x2(acc[n][0] * inv_l0, acc[n][1] * inv_l0);
        }
        if (qrow1 < tokens) {
            *reinterpret_cast<unsigned*>(&out[gqa_prefill_q_index<Geometry>(q_head, d0, qrow1)]) =
                pack_bf16x2(acc[n][2] * inv_l1, acc[n][3] * inv_l1);
        }
    }
#pragma unroll
    for (int s = 0; s < kGqaPrefillHeadsPerCta; ++s) {
        gqa_prefill_zero_output_rows<Geometry>(out, q_head_base + s, tokens, min(q0 + Br, width),
                                               tid, Threads);
    }
}

} // namespace ninfer::ops
